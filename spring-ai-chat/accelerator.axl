engine {

  applyTo("README.md" || "pom.xml" || "manifest.yml" || "src/main/resources/application.properties") {
    TextPreprocessor()
  }

  if (#vectorStoreType == 'simple') {
    Exclude({"**/schema-pgvector.sql", "**/application-pgvector.properties"})
  }
  else {
    Exclude({"**/SimpleVectorStoreConfig.java"})
  }

  applyTo("pom.xml") {
    ReplaceText({
      {text: "<groupId>com.vmware.tap.accelerators</groupId>", with: '<groupId>' + #groupId + '</groupId>'},
      {text: "<artifactId>ai-chat</artifactId>", with: '<artifactId>' + #artifactId + '</artifactId>'}
    })
    if (#aiService == 'azureOpenAI') {
      ReplaceSnippet(text: "spring-ai-starter-model-openai", with: 'spring-ai-starter-model-azure-openai')
    }
  }

  applyTo("src/main/resources/application.properties") {
    if (#aiService == 'openAI') {
      ReplaceSnippet(text: "spring.ai.openai.model=gpt-3.5-turbo", with: 'spring.ai.openai.model=' + #openaiModel)
    }
    if (#aiService == 'azureOpenAI') {
      ReplaceText({
        {text: "spring.ai.azure.openai.endpoint=TBD", with: 'spring.ai.azure.openai.endpoint=' + #azureAIEndpoint},
        {text: "spring.ai.openai.api-key", with: 'spring.ai.azure.openai.api-key'},
        {text: "spring.ai.openai.model=gpt-3.5-turbo", with: 'spring.ai.azure.openai.model=' + #openaiModel}
      })
    }
  }

  let packageDirectory = #packageName.replace(".", "/") in {
    applyTo("**/*.java") {
      ReplaceSnippet(text: "com.vmware.tap.accelerators.aichat", with: #packageName)
      RewritePath(regex: "src/(?<sourceset>.*)/java/(?<currentpackage>.*/)aichat(?<untouchedpath>.*)" , rewriteTo: 'src/' + #sourceset + '/java/' + #packageDirectory + '/' + #untouchedpath)
    }
  }

  // Update the artifact name as needed
  applyTo("README.md" || "manifest.yml") {
    ReplaceSnippet(text: "spring-ai-chat", with: #artifactId.toLowerCase())
  }

  // Set the profile as needed
  applyTo("manifest.yml") {
    if (#vectorStoreType == 'pgvector') {
        ReplaceRegex(pattern: "SPRING_PROFILES_ACTIVE: .*", with: 'SPRING_PROFILES_ACTIVE: "cloud,pgvector"')
    }
  }

  {
    Include({'**'})
    + InvokeFragment("build-wrapper-maven")
  }
  UniquePath(strategy: UseLast)

  // Create the provenance file
  Provenance()

}